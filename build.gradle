import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

group 'com.outdatedversion'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

def pluginEntryPoint = 'com.outdatedversion.Plugin'

repositories {
    mavenCentral()
    maven {
        url 'https://papermc.io/repo/repository/maven-public/'
    }
}

dependencies {
    compile group: 'com.github.berkesa', name: 'moleculer-java', version: '1.2.11'
    compile 'com.github.berkesa:datatree-promise:1.0.6'
    compile 'com.github.berkesa:datatree-core:1.0.14'
    compile group: 'io.nats', name: 'jnats', version: '2.6.8'
    compileOnly group: 'com.destroystokyo.paper', name: 'paper-api', version: '1.16.1-R0.1-SNAPSHOT'
}

processResources {
    filter ReplaceTokens, tokens: [
            main: pluginEntryPoint,
            version: project.version + '-' + getGitHash()
    ]
}

def getGitHash() {
    def sout = new ByteArrayOutputStream()

    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = sout
    }

    return sout.toString().trim()
}